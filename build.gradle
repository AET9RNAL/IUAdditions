buildscript {
    repositories {
        maven {
            name = ":forge"
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }

        jcenter()
        mavenCentral()

        flatDir {
            dirs 'lib'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        classpath "gradle.plugin.net.kyori:blossom:1.1.0"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "net.kyori.blossom"
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = modVersion
group = modGroup
archivesBaseName = modBaseName
//noinspection GroovyAssignabilityCheck
project.ic2_range= "[${project.ic2_min_ver},${project.ic2_max_ver}]"
compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}
blossom {
    replaceToken '%VERSION%', modVersion
    replaceToken "%IC2_RANGE%", ic2_range
    replaceToken "%CERTIFICATE%", fingerprint
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
}

minecraft {
    //noinspection GroovyAssignabilityCheck
    mappings channel: "snapshot", version: mcp_version
    runs {
        client {
            workingDirectory project.file('run')
            accessTransformer = file('src/main/resources/tc_at.cfg')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        data {
            property 'forge.logging.console.level', 'debug'
            workingDirectory project.file('run')
            args '--mod', 'ssp', '--all',
                    '--existing', '"' + file('src/main/resources/') + '"',
                    '--existing', '"' + file('src/generated/resources/') + '"',
                    '--output', '"' + file('src/generated/resources/') + '"'
        }
    }
}

repositories {

    maven {
        url "http://maven.cil.li"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    flatDir {
        dirs 'lib'
    }
}

dependencies {
    minecraft([
            group  : "net.minecraftforge",
            name   : "forge",
            version: "${minecraft_version}-${forge_version}"

    ])
//    compileOnly
//    runtimeOnly
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    compileOnly "curse.maven:industrialupgrade-372007:4998877"
    runtimeOnly "curse.maven:industrialupgrade-372007:4998877"
    compileOnly "curse.maven:astral-sorcery-241721:3044416"
    runtimeOnly "curse.maven:astral-sorcery-241721:3044416"
    runtimeOnly "curse.maven:botania-225643:2846950"
    compileOnly "curse.maven:botania-225643:2846950"
    compileOnly "curse.maven:forestry-59751:2684780"
    runtimeOnly "curse.maven:forestry-59751:2684780"
    runtimeOnly "curse.maven:shadowfacts-forgelin-248453:2785465"
    compileOnly "curse.maven:shadowfacts-forgelin-248453:2785465"
    runtimeOnly fileTree('lib')
    compileOnly fileTree('lib')
    compileOnly fileTree('libs')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft_version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft_version,
                "ic2_range": project.ic2_range
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}
def commonManifest = {
    attributes 'FMLAT': 'tc_at.cfg'
}

jar {
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "Aeternal",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"Aeternal",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Git-Commit-Hash": getGitHash()
        ])
    }
    from 'LICENSE'
    manifest commonManifest
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
}


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.incremental = true // one flag, and things will get so MUCH faster
    sourceCompatibility = targetCompatibility = '1.8'
}
jar.archiveFileName = "${project.archivesBaseName}-${project.minecraft_version}-${project.version}.jar"

jar.finalizedBy('reobfJar')
defaultTasks 'build'

static def getGitHash() {
    try {
        def process = 'git rev-parse --short HEAD'.execute()
        process.waitFor()
        return (process.exitValue() ? 'unknown' : process.text.trim())
    }catch(IOException ignored){
        return 'unknown'
    }
}

// Hack to include resources for Forge 1.12.2 using FG3
sourceSets { main { output.resourcesDir = output.classesDirs[0] } }
task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives devJar
    archives sourcesJar
}
